! =======================================================
! Τίτλος: Υπολογισμός Λογαριασμού Νερού
! =======================================================
! Περιγραφή:
! Πρόβλημα: Υπολογισμός τριμηνιαίου λογαριασμού νερού με κλιμακωτή χρέωση.
!
! Κλίμακες χρέωσης (ανά κυβικό μέτρο):
! - 0-15 κ.μ.: 0.34€
! - 16-60 κ.μ.: 0.52€
! - 61-81 κ.μ.: 1.51€
! - 82-105 κ.μ.: 2.11€
! - >105 κ.μ.: 2.64€
!
! Προσαυξήσεις: Πάγιο 1.47€, Αποχέτευση 40%, Άλλα 1%, ΦΠΑ 18%
! Δομές: Εντολή ΕΠΙΛΕΞΕ, βρόχος ΟΣΟ
! =======================================================

ΠΡΟΓΡΑΜΜΑ ΛΟΓΑΡΙΑΣΜΟΣ_ΝΕΡΟΥ
! Υπολογισμός λογαριασμού νερού με κλιμακωτή χρέωση

ΣΤΑΘΕΡΕΣ
  ΦΠΑ = 0.18
  ΤΙΜΗ1 = 0.34    ! 0-15 κ.μ.
  ΤΙΜΗ2 = 0.52    ! 16-60 κ.μ.
  ΤΙΜΗ3 = 1.51    ! 61-81 κ.μ.
  ΤΙΜΗ4 = 2.11    ! 82-105 κ.μ.
  ΤΙΜΗ5 = 2.64    ! >105 κ.μ.
  ΠΑΓΙΟ = 1.47

ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ: Μετρητής
  ΠΡΑΓΜΑΤΙΚΕΣ: Κατανάλωση, Τιμή, Αποχέτευση, Άλλα, Αξία_ΦΠΑ, Αξία, Τελική_Τιμή
  ΧΑΡΑΚΤΗΡΕΣ: Όνομα, Επώνυμο

ΑΡΧΗ
  ! Εισαγωγή αριθμού μετρητή (0 για έξοδο)
  ΓΡΑΨΕ 'Δώσε Αριθμό του μετρητή (0 για τέλος)'
  ΔΙΑΒΑΣΕ Μετρητής
  
  ! Επανάληψη για κάθε καταναλωτή
  ΟΣΟ Μετρητής <> 0 ΕΠΑΝΑΛΑΒΕ
    ΓΡΑΨΕ 'Δώσε το Ονοματεπώνυμο'
    ΔΙΑΒΑΣΕ Επώνυμο, Όνομα
    
    ΓΡΑΨΕ 'Δώσε την Κατανάλωση'
    ΔΙΑΒΑΣΕ Κατανάλωση
    
    ! Υπολογισμός αξίας νερού με κλιμακωτή χρέωση
    ΕΠΙΛΕΞΕ Κατανάλωση
      ΠΕΡΙΠΤΩΣΗ <= 15
        ! Μόνο πρώτη κλίμακα
        Αξία <- Κατανάλωση * ΤΙΜΗ1
        
      ΠΕΡΙΠΤΩΣΗ <= 60
        ! Πρώτη + δεύτερη κλίμακα
        Αξία <- 15 * ΤΙΜΗ1 + (Κατανάλωση - 15) * ΤΙΜΗ2
        
      ΠΕΡΙΠΤΩΣΗ <= 81
        ! Πρώτη + δεύτερη + τρίτη κλίμακα
        Αξία <- 15 * ΤΙΜΗ1 + 45 * ΤΙΜΗ2 + (Κατανάλωση - 60) * ΤΙΜΗ3
        
      ΠΕΡΙΠΤΩΣΗ <= 105
        ! Πρώτη + δεύτερη + τρίτη + τέταρτη κλίμακα
        Αξία <- 15*ΤΙΜΗ1 + 45*ΤΙΜΗ2 + 21*ΤΙΜΗ3 + (Κατανάλωση-81)*ΤΙΜΗ4
        
      ΠΕΡΙΠΤΩΣΗ_ΑΛΛΙΩΣ
        ! Όλες οι κλίμακες
        Αξία <- 15*ΤΙΜΗ1 + 45*ΤΙΜΗ2 + 21*ΤΙΜΗ3 + 24*ΤΙΜΗ4 + (Κατανάλωση-105)*ΤΙΜΗ5
    ΤΕΛΟΣ_ΕΠΙΛΟΓΩΝ
    
    ! Υπολογισμός επιβαρύνσεων
    Αποχέτευση <- Αξία * 0.4     ! 40% της αξίας
    Άλλα <- Αξία * 0.01          ! 1% της αξίας
    Τιμή <- Αξία + Αποχέτευση + Άλλα + ΠΑΓΙΟ
    Αξία_ΦΠΑ <- Τιμή * ΦΠΑ       ! 18% ΦΠΑ
    Τελική_τιμή <- Τιμή + Αξία_ΦΠΑ
    
    ! Εκτύπωση αποτελεσμάτων
    ΓΡΑΨΕ 'Ο λογαριασμός του', Επώνυμο,' είναι ', Τελική_τιμή
    ΓΡΑΨΕ 'Αξία νερού:', Αξία
    ΓΡΑΨΕ 'άλλα:', Αποχέτευση + Άλλα, 'ΦΠΑ:', Αξία_ΦΠΑ
    
    ! Επόμενος καταναλωτής
    ΓΡΑΨΕ 'Δώσε Αριθμό του επόμενου μετρητή (0 για τέλος)'
    ΔΙΑΒΑΣΕ Μετρητής
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
ΤΕΛΟΣ_ΠΡΟΓΡΑΜΜΑΤΟΣ
