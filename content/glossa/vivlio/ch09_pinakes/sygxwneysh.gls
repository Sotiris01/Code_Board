! =======================================================
! Τίτλος: Συγχώνευση Ταξινομημένων Πινάκων (Merge)
! =======================================================
! Περιγραφή:
! Πρόβλημα: Δίνονται δύο ταξινομημένοι πίνακες Α και Β με Ν και Μ
! στοιχεία αντίστοιχα. Να δημιουργηθεί ένας τρίτος πίνακας Γ με
! Ν+Μ ταξινομημένα στοιχεία.
!
! Αλγόριθμος:
! 1. Συγκρίνουμε τα τρέχοντα στοιχεία των Α και Β
! 2. Το μικρότερο τοποθετείται στον Γ
! 3. Αυξάνουμε τον αντίστοιχο δείκτη
! 4. Επαναλαμβάνουμε μέχρι να τελειώσει ένας πίνακας
! 5. Μεταφέρουμε τα υπόλοιπα του άλλου πίνακα
! =======================================================

ΠΡΟΓΡΑΜΜΑ Συγχώνευση
! Συγχώνευση δύο ταξινομημένων πινάκων σε έναν τρίτο

ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ: Α[100], Β[100], Γ[200], I, J, Κ, Ν, Μ, Λ
  ! Α και Β: αρχικοί ταξινομημένοι πίνακες
  ! Γ: τελικός ταξινομημένος πίνακας

ΑΡΧΗ
  ! Εισαγωγή πρώτου πίνακα
  ΓΡΑΨΕ 'Δώσε το πλήθος των στοιχείων του πίνακα Α (<100)'
  ΔΙΑΒΑΣΕ Ν
  ΓΙΑ Ι ΑΠΟ 1 ΜΕΧΡΙ Ν
    ΔΙΑΒΑΣΕ Α[I]
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  
  ! Εισαγωγή δεύτερου πίνακα
  ΓΡΑΨΕ 'Δώσε το πλήθος των στοιχείων του πίνακα Β (<100)'
  ΔΙΑΒΑΣΕ Μ
  ΓΙΑ Ι ΑΠΟ 1 ΜΕΧΡΙ Μ
    ΔΙΑΒΑΣΕ Β[I]
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  
  ! Αρχικοποίηση δεικτών
  ! I: δείκτης για τον πίνακα Α
  ! J: δείκτης για τον πίνακα Β
  ! Κ: δείκτης για τον πίνακα Γ
  I <- 1
  J <- 1
  Κ <- 1
  
  ! Συγχώνευση: Όσο και οι δύο πίνακες έχουν στοιχεία
  ΟΣΟ I <= Ν ΚΑΙ J <= Μ ΕΠΑΝΑΛΑΒΕ
    ! Βάλε το μικρότερο στον Γ
    ΑΝ Α[Ι] < B[J] ΤΟΤΕ
      Γ[Κ] <- Α[I]
      Κ <- Κ + 1
      I <- Ι + 1
    ΑΛΛΙΩΣ
      Γ[Κ] <- B[J]
      Κ <- Κ + 1
      J <- J + 1
    ΤΕΛΟΣ_ΑΝ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  
  ! Μεταφορά υπολοίπων στοιχείων του Α ή του Β
  ΑΝ I > Ν ΤΟΤΕ
    ! Τελείωσε ο Α, μετάφερε τα υπόλοιπα του Β
    ΓΙΑ Λ ΑΠΟ Κ ΜΕΧΡΙ Ν + Μ
      Γ[Λ] <- B[J]
      J <- J + 1
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΑΛΛΙΩΣ
    ! Τελείωσε ο Β, μετάφερε τα υπόλοιπα του Α
    ΓΙΑ Λ ΑΠΟ Κ ΜΕΧΡΙ Ν + Μ
      Γ[Λ] <- Α[I]
      I <- Ι + 1
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΤΕΛΟΣ_ΑΝ
  
  ! Εκτύπωση τελικού πίνακα
  ΓΙΑ Λ ΑΠΟ 1 ΜΕΧΡΙ Ν + Μ
    ΓΡΑΨΕ Γ[Λ]
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
ΤΕΛΟΣ_ΠΡΟΓΡΑΜΜΑΤΟΣ
